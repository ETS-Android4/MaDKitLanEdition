/*
 * This build file was generated by the Gradle 'init' task.
 *
 * This generated file contains a sample Java Library project to get you started.
 * For more details take a look at the Java Libraries chapter in the Gradle
 * user guide available at https://docs.gradle.org/4.1/userguide/java_library_plugin.html
 */

// Apply the java-library plugin to add support for Java Library
plugins {
    id "java-library"
    id "maven-publish"
    id "eclipse"
    id "idea"
    id "signing"
    //id "org.owasp.dependencycheck" version "6.1.0"
}


Properties props = new Properties()
def propFile = new File(project.getProjectDir().getParentFile().getParentFile(), 'credentials.properties')
def artifactory_Url="https://artifactory.distri-mind.fr/artifactory"
def user=""
def pwd=""
if (propFile.canRead())
{
    props.load(new FileInputStream(propFile))


    if (props!=null && props.containsKey('artifactory_contextUrl'))
        artifactory_Url=props.get("artifactory_contextUrl")
    if (props!=null && props.containsKey('artifactory_user'))
        user=props.get("artifactory_user")
    if (props!=null && props.containsKey('artifactory_password'))
        pwd=props.get("artifactory_password")
}
eclipse {
    classpath {
       downloadSources=true
    }
}

repositories {


   	maven {
       		url "${artifactory_Url}/gradle-release"
       		if (user!="")
       		{
	       		credentials {
	              username "${user}"
	              password "${pwd}"
	            }
	         }
   	}
    maven {
        url 'https://repo.maven.apache.org/maven2'
        name 'Maven Central'
    }
}


dependencies {
	testImplementation 'org.testng:testng:7.4.0'
    //testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.hamcrest:hamcrest-core:2.2'
    api(group:'com.distrimind.ood', name: 'OOD', version: '3.1.7-STABLE')

    //api project(':OOD')
    api(group:'org.fourthline.cling', name: 'cling-core', version: '2.1.2')
    api(group:'org.fourthline.cling', name: 'cling-support', version: '2.1.2')
    testImplementation(group:'com.h2database', name:'h2', version:'1.4.200')

     
}
def javaVersion="8"
sourceCompatibility = javaVersion
targetCompatibility= javaVersion
version='??.??.??ToDefine'


task generateVersionsFile(type:JavaExec, dependsOn:classes) {
    if (project.hasProperty('assemble') || project.hasProperty('build') || project.hasProperty('jar')) {
        def versionBuildFile = new File(project.getProjectDir(), 'src/main/resources/com/distrimind/madkit/kernel/build.txt')

        /*Wrapping inside a method avoids auto incrementing on every gradle task run. Now it runs only when we build apk*/
        if (versionBuildFile.canRead()) {
            def line = 0
            versionBuildFile.withReader { line = it.readLine() }
            def buildNumber = line.toInteger() + 1
            versionBuildFile.withWriter { out ->
                out.println buildNumber
            }
        } else {
            versionBuildFile.withWriter { out ->
                out.println 1
            }
        }

        getMainClass().set( 'com.distrimind.madkit.kernel.VersionsFileGenerator' )
        classpath = sourceSets.main.runtimeClasspath

    }
}
task generateMDFiles (dependsOn: generateVersionsFile) {

    if (project.hasProperty('assemble') || project.hasProperty('build') || project.hasProperty('jar')) {
        doLast {
            def lastVersionFile = new File(project.getProjectDir().getParentFile(), 'lastVersion.md')


            project.version = lastVersionFile.getText('UTF-8').trim()

            def mdFile = new File(project.getProjectDir().getParentFile(), 'README_TO_EDIT.md')
            if (mdFile.canRead()) {
                def mdFileDest = new File(project.getProjectDir().getParentFile(), 'README.md')

                mdFileDest.newWriter().withWriter { w ->
                    w << mdFile.getText('UTF-8').replaceAll("//PROJECT_VERSION//", version.toString())

                }
            }
            ext.artifactVersion = String.valueOf(project.version)
            def pomFile = new File(project.getProjectDir(), 'build/publications/mavenJava/pom-default.xml')
            if (pomFile.canRead()) {
                String t = pomFile.getText('UTF-8').replaceAll("\\?\\?.\\?\\?.\\?\\?ToDefine", version.toString())

                pomFile.newWriter().withWriter { w ->
                    w << t
                }
            }


        }

    }
}

allprojects {
	group='com.distrimind.madkit'
    
    //compileJava.options.compilerArgs << "-Xlint"

    if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_1_9)) {
        compileJava.options.compilerArgs.addAll(['--release', javaVersion] as Collection<? extends String>)
        javadoc.options.addStringOption("html5", "-quiet")
    }

}

idea {
    project {
        jdkName = javaVersion
        languageLevel = javaVersion
    }
}


task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
    from project.getProjectDir().getParentFile().getAbsolutePath()+"LICENSE"
    from project.getProjectDir().getParentFile().getAbsolutePath()+"changelog.md"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
    from project.getProjectDir().getParentFile().getAbsolutePath()+"LICENSE"
    from project.getProjectDir().getParentFile().getAbsolutePath()+"changelog.md"
}

artifacts {
    archives sourcesJar
    archives javadocJar
}




jar{
	dependsOn generateMDFiles
    manifest {
        attributes('Implementation-Title': project.name,
                   'Implementation-Version': project.version)
    }
    from project.getProjectDir().getParentFile().getAbsolutePath()+"LICENSE"
    from project.getProjectDir().getParentFile().getAbsolutePath()+"changelog.md"

}



test {
	
	// enable TestNG support (default is JUnit)
   	useTestNG
   	{
   		suites 'src/test/java/com/distrimind/madkit/AllTestsNG.xml'
   	}
   
   // set heap size for the test JVM(s)
   minHeapSize = "256m"
   maxHeapSize = "2g"
 

	test {
    		afterTest { desc, result -> 
        		logger.quiet "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
    		}
	}
   
 }

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'MaDKitLanEdition'
                description = 'MaDKit is an open source multiagent-based development platform written in Java. MaDKitLanEdition Advanced network futures which enables to connect several peers between them, and emulate a secured multi-agent system that virtually evolves into the same environment, i.e. the same virtual machine.'
                url = 'https://github.com/JazZ51/MaDKitLanEdition'
                developers {
                    developer {
                        id = 'mahdjoub'
                        name = 'Jason Mahdjoub'
                        email = 'jason.mahdjoub@distri-mind.fr'
                    }
                    developer {
                        id = 'michel'
                        name = 'Fabien Michel'
                    }
                    developer {
                        id = 'gutknecht'
                        name = 'Olivier Gutknecht'
                        email = 'jason.mahdjoub@distri-mind.fr'
                    }
                    developer {
                        id = 'ferber'
                        name = 'Jacques Ferber'
                        email = 'jason.mahdjoub@distri-mind.fr'
                    }
                }

                organization {
                    name = 'DistriMind'
                    url = 'https://github.com/JazZ51'
                }
                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/JazZ51/MaDKitLanEdition/issues'
                }
                scm {
                    connection = 'https://github.com/JazZ51/MaDKitLanEdition'
                }
                licenses {
                    license {
                        name = 'CeCILL-C FREE SOFTWARE LICENSE AGREEMENT'
                        url = 'http://cecill.info/licences/Licence_CeCILL-C_V1-en.txt'
                    }
                }

            }
        }
    }
    repositories {
        maven {
            credentials {
                username "${user}"
                password "${pwd}"
            }
            url "${artifactory_Url}/DistriMind-Public"
        }
    }
}
signing {
    useGpgCmd()
    required {!project.hasProperty('disableSigning')}
    if (required) {
        sign configurations.archives
        sign publishing.publications.mavenJava
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}




