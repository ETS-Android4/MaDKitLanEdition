/*
 * This build file was generated by the Gradle 'init' task.
 *
 * This generated file contains a sample Java Library project to get you started.
 * For more details take a look at the Java Libraries chapter in the Gradle
 * user guide available at https://docs.gradle.org/4.1/userguide/java_library_plugin.html
 */

// Apply the java-library plugin to add support for Java Library
plugins {
    id "java-library"
    id "maven-publish"
    id "eclipse"
    id "idea"
    id "signing"
    //id "org.owasp.dependencycheck" version "6.1.0"
}


Properties props = new Properties()
def propFile = new File(project.getProjectDir().getParentFile().getParentFile().getParentFile(), 'credentials.properties')
def artifactory_Url="https://artifactory.distri-mind.fr/artifactory"
def user=""
def pwd=""
if (propFile.canRead())
{
    props.load(new FileInputStream(propFile))


    if (props!=null && props.containsKey('artifactory_contextUrl'))
        artifactory_Url=props.get("artifactory_contextUrl")
    if (props!=null && props.containsKey('artifactory_user'))
        user=props.get("artifactory_user")
    if (props!=null && props.containsKey('artifactory_password'))
        pwd=props.get("artifactory_password")
}
eclipse {
    classpath {
        downloadSources=true
    }
}

repositories {


    maven {
        url "${artifactory_Url}/gradle-release"
        if (user!="")
        {
            credentials {
                username "${user}"
                password "${pwd}"
            }
        }
    }
    maven {
        url 'https://repo.maven.apache.org/maven2'
        name 'Maven Central'
    }
}

def javaVersion="8"
sourceCompatibility = javaVersion
targetCompatibility= javaVersion
def lastVersionFile = new File(project.getProjectDir().getParentFile().getParentFile(), 'lastVersion.md')
version = lastVersionFile.getText('UTF-8').trim()
//version='??.??.??ToDefine'

dependencies {
    testImplementation 'org.testng:testng:7.5'
    var slf4jVersion='1.7.35'
    testImplementation 'org.slf4j:slf4j-api:'+slf4jVersion
    testImplementation 'org.slf4j:slf4j-simple:'+slf4jVersion

    //testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.hamcrest:hamcrest-core:2.2'

    testImplementation(group:'com.h2database', name:'h2', version:'2.1.210')
    testImplementation 'com.distrimind.madkit:MaDKitLanEdition:'+version

}


task generateMDFiles (dependsOn: classes) {

    if (project.hasProperty('assemble') || project.hasProperty('build') || project.hasProperty('jar')) {
        doLast {


            ext.artifactVersion = String.valueOf(project.version)
            def pomFile = new File(project.getProjectDir(), 'build/publications/mavenJava/pom-default.xml')
            if (pomFile.canRead()) {
                String t = pomFile.getText('UTF-8').replaceAll("\\?\\?.\\?\\?.\\?\\?ToDefine", version.toString())

                pomFile.newWriter().withWriter { w ->
                    w << t
                }
            }

        }

    }
}

allprojects {
    group='com.distrimind.madkit.swing'

    //compileJava.options.compilerArgs << "-Xlint"

    if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_1_9)) {
        compileJava.options.compilerArgs.addAll(['--release', javaVersion] as Collection<? extends String>)
        javadoc.options.addStringOption("html5", "-quiet")
    }

}



task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
    from project.getProjectDir().getParentFile().getParentFile().getAbsolutePath()+"LICENSE"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
    from project.getProjectDir().getParentFile().getParentFile().getAbsolutePath()+"LICENSE"
}

artifacts {
    archives sourcesJar
    archives javadocJar
}




jar{
    dependsOn generateMDFiles
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }
    from project.getProjectDir().getParentFile().getParentFile().getAbsolutePath()+"LICENSE"


}



test {

    // enable TestNG support (default is JUnit)
    useTestNG
            {
                suites 'src/test/java/com/distrimind/madkit/swing/AllTestsNG.xml'
            }

    // set heap size for the test JVM(s)
    minHeapSize = "256m"
    maxHeapSize = "2g"


    test {
        afterTest { desc, result ->
            logger.quiet "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
        }
    }

}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'MaDKitLanEdition - Swing driver'
                description = 'MaDKit is an open source multiagent-based development platform written in Java. Swing driver add ability to MaDKit to use graphical interface.'
                url = 'https://github.com/JasonMahdjoub/MaDKitLanEdition'
                developers {
                    developer {
                        id = 'mahdjoub'
                        name = 'Jason Mahdjoub'
                        email = 'jason.mahdjoub@distri-mind.fr'
                    }
                    developer {
                        id = 'michel'
                        name = 'Fabien Michel'
                    }
                    developer {
                        id = 'gutknecht'
                        name = 'Olivier Gutknecht'
                        email = 'jason.mahdjoub@distri-mind.fr'
                    }
                    developer {
                        id = 'ferber'
                        name = 'Jacques Ferber'
                        email = 'jason.mahdjoub@distri-mind.fr'
                    }
                }

                organization {
                    name = 'DistriMind'
                    url = 'https://github.com/JasonMahdjoub'
                }
                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/JasonMahdjoub/MaDKitLanEdition/issues'
                }
                scm {
                    connection = 'https://github.com/JasonMahdjoub/MaDKitLanEdition'
                }
                licenses {
                    license {
                        name = 'CeCILL-C FREE SOFTWARE LICENSE AGREEMENT'
                        url = 'http://cecill.info/licences/Licence_CeCILL-C_V1-en.txt'
                    }
                }

            }
        }
    }
    repositories {
        maven {
            credentials {
                username "${user}"
                password "${pwd}"
            }
            url "${artifactory_Url}/DistriMind-Public"
        }
    }
}
signing {
    useGpgCmd()
    required {!project.hasProperty('disableSigning')}
    if (required) {
        sign configurations.archives
        sign publishing.publications.mavenJava
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}




