/*
 * This build file was generated by the Gradle 'init' task.
 *
 * This generated file contains a sample Java Library project to get you started.
 * For more details take a look at the Java Libraries chapter in the Gradle
 * user guide available at https://docs.gradle.org/4.1/userguide/java_library_plugin.html
 */

// Apply the java-library plugin to add support for Java Library
plugins {
    id "java"
    id "maven-publish"
    id "eclipse"
    id "idea"
    id "signing"
    id "application"
    //id "org.owasp.dependencycheck" version "6.1.0"
}

Properties props = new Properties()
def propFile = new File(project.getProjectDir().getParentFile().getParentFile(), 'credentials.properties')
def artifactoryUrl="https://artifactory.distri-mind.fr:443/artifactory"
def sonatypeStagingUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
def artifactoryUser=""
def artifactoryPwd=""
def sonatypeUser=""
def sonatypePwd=""

if (propFile.canRead())
{
    props.load(new FileInputStream(propFile))


    if (props!=null && props.containsKey('artifactory_contextUrl'))
        artifactoryUrl=props.get("artifactory_contextUrl")
    if (props!=null && props.containsKey('artifactory_user'))
        artifactoryUser=props.get("artifactory_user")
    if (props!=null && props.containsKey('artifactory_password'))
        artifactoryPwd=props.get("artifactory_password")
    if (props!=null && props.containsKey('sonatype_staging_url'))
        sonatypeStagingUrl=props.get("sonatype_staging_url")
    if (props!=null && props.containsKey('sonatype_user'))
        sonatypeUser=props.get("sonatype_user")
    if (props!=null && props.containsKey('sonatype_password'))
        sonatypePwd=props.get("sonatype_password")
}
eclipse {
    classpath {
       downloadSources=true
    }
}

// In this section you declare where to find the dependencies of your project
repositories {


    maven {
        url "${artifactoryUrl}/gradle-release/"
        if (artifactoryUser!="")
        {
            credentials {
                username "${artifactoryUser}"
                password "${artifactoryPwd}"
            }
        }
    }
}

def f = new File(project.getProjectDir().getParentFile(), 'lastVersion.md')
var lastMkVersionFile = f.getText('UTF-8').trim()
dependencies {
    implementation 'com.distrimind.madkit:MaDKitLanEdition:'+lastMkVersionFile
    implementation 'com.distrimind.madkit.gui.swing:MadkitSwingDriver:'+lastMkVersionFile
    
     
}
def javaVersion="8"
sourceCompatibility = javaVersion
targetCompatibility= javaVersion
allprojects {
	group='com.distrimind.madkitdemos'
    
    mainClassName = "com.distrimind.madkitdemos.MKLEDemos"

    //compileJava.options.compilerArgs << "-Xlint"
    if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_1_9)) {
        compileJava.options.compilerArgs.addAll(['--release', javaVersion] as Collection<? extends String>)
        javadoc.options.addStringOption("html5", "-quiet")
    }
}


version='1.0.41'


task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
    from "../LICENSE"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
    from "../LICENSE"
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                   'Implementation-Version': project.version,
                   'Main-Class': project.mainClassName)
    }
    from "../LICENSE"

}

test {
	
	// enable TestNG support (default is JUnit)
   	useTestNG
   	{
   		suites 'src/test/java/com/distrimind/madkit/AllTestsNG.xml'
   	}
   
   // set heap size for the test JVM(s)
   minHeapSize = "256m"
   maxHeapSize = "2g"
   
   
   
 }


publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'DÃ©mos for MaDKitLanEdition'
                description = 'MaDKit is an open source multiagent-based development platform written in Java. MaDKitLanEdition Advanced network futures which enables to connect several peers between them, and emulate a secured multi-agent system that virtually evolves into the same environment, i.e. the same virtual machine.'
                url = 'https://github.com/JasonMahdjoub/MaDKitLanEdition'
                developers {
                    developer {
                        id = 'jmahdjoub'
                        name = 'Jason Mahdjoub'
                        email = 'jason.mahdjoub@distri-mind.fr'
                        organization = 'DistriMind'
                        roles.add('Entrepreneur')
                        roles.add('Developer')
                    }
                    developer {
                        name = 'Fabien Michel'
                        email = 'fmichel@lirmm.fr'
                        organization = 'LIRMM - University Montpellier'
                    }
                    developer {
                        name = 'Olivier Gutknecht'
                    }
                    developer {
                        name = 'Jacques Ferber'
                    }
                }

                organization {
                    name = 'DistriMind'
                    url = 'https://github.com/JasonMahdjoub'
                }
                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/JasonMahdjoub/MaDKitLanEdition/issues'
                }
                scm {
                    connection = 'https://github.com/JasonMahdjoub/MaDKitLanEdition'
                    developerConnection = connection
                    url = connection
                }
                licenses {
                    license {
                        name = 'CeCILL-C FREE SOFTWARE LICENSE AGREEMENT'
                        url = 'http://cecill.info/licences/Licence_CeCILL-C_V1-en.txt'
                    }
                }

            }
        }
    }
    repositories {
        maven {
            credentials {
                username "${artifactoryUser}"
                password "${artifactoryPwd}"
            }
            url "${artifactoryUrl}/DistriMind-Public"
        }
        maven {
            credentials {
                username "${sonatypeUser}"
                password "${sonatypePwd}"
            }
            url "${sonatypeStagingUrl}"
        }
    }
}
signing {
    useGpgCmd()
    required {!project.hasProperty('disableSigning')}
    if (required) {
        sign configurations.archives
        sign publishing.publications.mavenJava
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

